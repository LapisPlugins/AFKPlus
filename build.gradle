/*
 * Copyright 2025 Benjamin Martin
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'java'
    id 'maven-publish'
    id "com.gradleup.shadow" version "9.0.0-rc1"
}

group = 'net.lapismc'
archivesBaseName = "AFKPlus"
version = '3.4.9'
description = 'AFK for professional servers'
sourceCompatibility = '21'
targetCompatibility = '21'
defaultTasks 'clean', 'build', 'shadowJar'

repositories {
    mavenCentral()
    //Paper Repo
    maven {
        url = 'https://repo.papermc.io/repository/maven-public/'
    }
    //Personal Repo
    maven {
        name = "lapismc-repo"
        url = "https://maven.lapismc.net/repository/maven/"
    }
    //Essentials Repo
    maven {
        url = uri('https://repo.essentialsx.net/releases/')
    }
    //DiscordSRV Repo
    maven {
        url 'https://nexus.scarsz.me/content/groups/public/'
    }
}

dependencies {
    implementation 'net.lapismc:LapisCore:1.13.7'
    implementation 'org.ocpsoft.prettytime:prettytime:5.0.8.Final'
    compileOnly 'net.essentialsx:EssentialsX:2.20.1'
    compileOnly 'net.luckperms:api:5.4'
    compileOnly 'com.discordsrv:discordsrv:1.29.0'
}

processResources {
    filter {
        it.replace('${project.version}', project.getVersion())
    }
}

shadowJar {
    archiveFileName.set(archivesBaseName + "-" + project.getVersion() + ".jar")
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    exclude 'javax/**'
    relocate("org.ocpsoft.prettytime", "net.lapismc.afkplus.util.prettytime")
    relocate("net.lapismc.lapiscore", "net.lapismc.afkplus.util.core")
}

javadoc {
    source = sourceSets.main.allJava
    include("net/lapismc/afkplus/api/*.java")
    include("net/lapismc/afkplus/playerdata/*.java")
}

tasks.register('javadocJar', Jar) {
    archiveClassifier.set('javadoc')
    from javadoc
}

tasks.register('sourcesJar', Jar) {
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar, shadowJar
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

java {
    withSourcesJar()
    withJavadocJar()
}

publishing {
    publications {
        shadow(MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            name 'nexus'
            url "https://nexus.dnablue2112.com/repository/maven-releases/"
            credentials {
                username repoUser
                password repoPassword
            }
        }
    }
}